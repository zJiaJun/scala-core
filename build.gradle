group = 'com.github.zjiajun'
version = 1.0

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url "http://maven.aliyun.com/nexus/content/groups/public"
		}
	}
	apply from: "$rootDir/gradle/dependencies.gradle"

}
tasks.named('wrapper') {
	distributionType = Wrapper.DistributionType.ALL
}


allprojects {
	repositories {
		mavenLocal()
		maven {
			url "http://maven.aliyun.com/nexus/content/groups/public"
		}
	}

	apply plugin: 'idea'
}


subprojects {

	apply plugin: 'java'
	apply plugin: 'scala'

	tasks.withType(ScalaCompile) {
		scalaCompileOptions.forkOptions.with {
			memoryMaximumSize = '1g'
			jvmArgs = ['-Xss2m','-XX:+HeapDumpOnOutOfMemoryError','-XX:HeapDumpPath=/tmp/heapdump.hprof']
		}
	}

	dependencies {
		implementation libs.scalaLibrary
		implementation libs.scalaReflect
		implementation libs.scalaLogging
		implementation libs.slf4jApi
		implementation libs.log4j2
		implementation libs.slf4jlog4j2
		testImplementation libs.scalatest
	}
}

project(':core') {
	archivesBaseName = "core"
}
project(':scalikejdbc') {

	dependencies {
		implementation libs.mysql
		implementation libs.scalikejdbc
	}
}


