group = 'com.github.zjiajun'
version = 1.0

buildscript {
	repositories {
		mavenLocal()
		maven {
			url "http://maven.aliyun.com/nexus/content/groups/public"
		}
		mavenCentral()
	}
	apply from: "$rootDir/gradle/dependencies.gradle"

	dependencies {
		classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotlessPlugin"
	}
}

apply plugin: "com.diffplug.gradle.spotless"
spotless {
	scala {
		target '**/*.scala'
		scalafmt("$versions.scalafmt").configFile('checkstyle/.scalafmt.conf')
	}
}


tasks.named('wrapper') {
	gradleVersion = '6.5'
	distributionType = Wrapper.DistributionType.ALL
}


allprojects {
	repositories {
		mavenLocal()
		maven {
			url "http://maven.aliyun.com/nexus/content/groups/public"
		}
	}

	apply plugin: 'idea'
}


subprojects {

	apply plugin: 'java'
	apply plugin: 'scala'

	tasks.withType(ScalaCompile) {
		scalaCompileOptions.forkOptions.with {
			memoryMaximumSize = '1g'
			jvmArgs = ['-Xss2m','-XX:+HeapDumpOnOutOfMemoryError','-XX:HeapDumpPath=/tmp/heapdump.hprof']
		}
	}

	dependencies {
		implementation libs.scalaLibrary
		implementation libs.scalaReflect
		implementation libs.scalaLogging
		implementation libs.slf4jApi
		implementation libs.log4j2
		implementation libs.slf4jlog4j2
		testImplementation libs.scalatest
	}
}

project(':core') {
	archivesBaseName = "core"
}
project(':scalike') {

	dependencies {
		implementation libs.mysql
		implementation libs.scalikejdbc
		implementation libs.scalikejdbcConfig
		implementation libs.hikariCP
	}
}


